plugins {
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'com.github.node-gradle.node' version '7.0.1'
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'        // REST API
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'   // Database access
    implementation 'org.springframework.boot:spring-boot-starter-validation' // Request validation

    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client' // Database driver

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    jvmArgs '-XX:-UseSharedSpaces'
}

def skipFrontend = project.hasProperty('skipFrontend') && project.property('skipFrontend') == 'true'

node {
    version = '18.17.0'
    npmVersion = '9.6.7'
    download = true
    nodeProjectDir = file("${project.projectDir}/frontend")
}

def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')
def npmExecutable = isWindows ? 'npm.cmd' : 'npm'

tasks.register('installFrontendDeps', Exec) {
    workingDir 'frontend'
    executable npmExecutable
    args 'ci'
    inputs.file('frontend/package-lock.json')
    outputs.dir('frontend/node_modules')
}

tasks.register('testFrontend', Exec) {
    dependsOn tasks.installFrontendDeps
    workingDir 'frontend'
    executable npmExecutable
    args 'run', 'test:unit'
    onlyIf { !skipFrontend }
}

tasks.register('buildFrontend', Exec) {
    dependsOn tasks.installFrontendDeps
    workingDir 'frontend'
    executable npmExecutable
    args 'run', 'build'
    inputs.files(fileTree("frontend/src"))
    inputs.file("frontend/package.json")
    outputs.dir("frontend/dist")
    onlyIf { !skipFrontend }
}

tasks.register('copyFrontend', Copy) {
    dependsOn tasks.buildFrontend
    from "frontend/dist"
    into "src/main/resources/static"
    onlyIf { !skipFrontend }
}

// Configure main tasks
tasks.named('test') {
    if (!skipFrontend) {
        dependsOn tasks.testFrontend
    }
}

tasks.named('processResources') {
    if (!skipFrontend) {
        dependsOn tasks.copyFrontend
    }
}